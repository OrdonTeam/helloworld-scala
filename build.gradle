apply plugin: 'scala'
apply plugin: 'cobertura'
apply plugin: 'coveralls'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.saliman', name: 'gradle-cobertura-plugin', version: '2.2.2'
        classpath group: 'org.kt3k.gradle.plugin', name: 'coveralls-gradle-plugin', version: '0.4.0'
    }
}

version = '0.0.1'
cobertura.coverageFormats = ['html', 'xml']

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.10.4'
    testCompile group: 'org.scalatest', name: 'scalatest_2.10', version: '2.1.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task makeDirs << {
    def sources = [sourceSets.main, sourceSets.test]
    sources*.allSource*.srcDirs.flatten().each { File srcDir ->
        println "Making $srcDir"
        srcDir.mkdirs()
    }
}

task jarWithDependencies(type: Jar, dependsOn: build) {
    appendix = "with-dependencies"
    manifest { attributes 'Main-Class': 'HelloWorld' }
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }}
}

